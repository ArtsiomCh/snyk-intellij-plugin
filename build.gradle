buildscript {
    ext.scala_bin_version = '2.12.6'
    ext.scala_version = '2.12'
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.3.5'
}

group 'io.snyk'
description = 'Snyk security scanning for IntelliJ IDEA'
version '0.0.7'

apply plugin: 'idea'
apply plugin: 'scala'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.scala-lang:scala-library:$scala_bin_version"
    compile "org.scala-lang:scala-reflect:$scala_bin_version"
    compile "org.nanohttpd:nanohttpd:2.3.1"
    compile "org.nanohttpd:nanohttpd-websocket:2.3.1"
    compile("com.github.jknack:handlebars:4.1.0") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile "io.monix:monix_$scala_version:3.0.0-RC1"
    compile "com.softwaremill.sttp:core_$scala_version:1.2.3"
    compile "com.beachape:enumeratum_$scala_version:1.5.13"
//    compile "org.scalafx:scalafx_$scala_version:8.0.144-R12"
    compile "io.circe:circe-core_$scala_version:0.9.3"
    compile "io.circe:circe-parser_$scala_version:0.9.3"
    compile "io.circe:circe-java8_$scala_version:0.9.3"
    compile "io.circe:circe-derivation_$scala_version:0.9.0-M5"

    testCompile "com.lihaoyi:pprint_$scala_version:0.5.3"
    testCompile "com.github.nikita-volkov:sext:0.2.4"
    testCompile "junit:junit:4.12"
}

intellij {
    type 'IC'
    //version '2017.3'
    //see https://www.jetbrains.com/intellij-repository/releases for versions
    version '2018.2.2'
    //version '182.3684.40' // 2018-07-17 release of 2018.2 EAP
    pluginName 'snyk-intellij-plugin'
    plugins = ['maven', 'gradle']
    downloadSources = true
    intellij.updateSinceUntilBuild false

    publishPlugin {
        username project.properties['intellijPublishUsername'] ?: "snyksec"
        password project.properties['intellijPublishPassword'] ?: "<not public>"
        channels 'develop'
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task previewHtmlUi(type:JavaExec) {
    main = "PreviewHtmlUi"
    classpath = sourceSets.test.runtimeClasspath
}
